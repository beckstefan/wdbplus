(: wdbplus: load parts of start.html by means of project specifics
 : created: 2019-02-20
 : creator: DK - Dario Kampkaspar <dario.kampkaspar@oeaw.ac.at>
 : sources: https://github.com/dariok/wdbplus
 : changes:
 :          - 2019-06-08: module now used in function.html context
 :          - 2019-06-28: enable use of templating functions within project
 :                        specific HTML files
 :)
xquery version "3.0";

module namespace wdbst = "https://github.com/dariok/wdbplus/start";

import module namespace wdbRc     = "https://github.com/dariok/wdbplus/RestCollections" at "../rest/rest-coll.xql";
import module namespace templates = "http://exist-db.org/xquery/templates"  at "/db/apps/shared-resources/content/templates.xql";
import module namespace wdb       = "https://github.com/dariok/wdbplus/wdb" at "app.xqm";

declare namespace output = "http://www.w3.org/2010/xslt-xquery-serialization";
declare namespace wdbPF  = "https://github.com/dariok/wdbplus/projectFiles";

declare option output:method "html5";
declare option output:media-type "text/html";

(: get the left part of the start page from either projectSpec HTML, projectSpec function or a generic heading :)
declare function wdbst:getStartLeft($node as node(), $model as map(*)) as node()* {
  let $projectAvailable := wdb:findProjectXQM($model?pathToEd)
  let $functionsAvailable := if ($projectAvailable)
    then util:import-module(xs:anyURI("https://github.com/dariok/wdbplus/projectFiles"), 'wdbPF',
        xs:anyURI($projectAvailable))
    else false()
    
  return if (doc-available($model("projectResources") || '/startLeft.html'))
  then templates:apply(doc($model("projectResources") || '/startLeft.html'),  $wdbst:lookup, $model)
  else if (wdb:findProjectFunction($model, 'getStartLeft', 1))
  then wdb:eval('wdbPF:getStartLeft($model)', false(), (xs:QName('model'), $model))
  else (<h1>Inhalt</h1>, wdbRc:getCollectionNavHTML($model?id))
  (: this is currently necessary due to some eXist bug that throws an error when trying to open the REST URL via doc() or unparsed-text() :)
};

(: get the main part of the start page from either projectSpec HTML, projectSpec function or return an empty seq :)
declare function wdbst:getStart ($node as node(), $model as map(*)) as node()* {
  let $projectAvailable := wdb:findProjectXQM($model?pathToEd)
  let $functionsAvailable := if ($projectAvailable)
    then util:import-module(xs:anyURI("https://github.com/dariok/wdbplus/projectFiles"), 'wdbPF',
        xs:anyURI($projectAvailable))
    else false()
    
  return if (doc-available($model("projectResources") || '/startRight.html'))
  then templates:apply(doc($model("projectResources") || '/startRight.html'), $wdbst:lookup, $model)
  else if (wdb:findProjectFunction($model, 'getStart', 1))
  then wdb:eval('wdbPF:getStart($model)', false(), (xs:QName('model'), $model))
  else ()
};

(: header is generated by function.xqm :)

(: we need a lookup function for the templating system to work :)
declare variable $wdbst:lookup := function($functionName as xs:string, $arity as xs:int) {
    try {
        function-lookup(xs:QName($functionName), $arity)
    } catch * {
        ()
    }
}; 